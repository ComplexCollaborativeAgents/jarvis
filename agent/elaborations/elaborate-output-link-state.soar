##!
# @file
#
# @created smohan 20180427

sp {elaborate*output-link*clear
    :o-support
    (state <s>    ^name jarvis
                  ^io.output-link <ol>)
    (<ol>    ^{<< state next-instruction >> <any>} <c>)
    (<c>    ^status complete)
-->
    (<ol>    ^<any> <c> -)
    }

sp {elaborate*output-link*state*command
	:o-support
	(state <s>	^name jarvis
            	^io <io>)
 	(<io> 	^output-link <ol>
         	^input-link <il>)
  	(<il>	^time <t>
         	^interaction.request get-all)
-->
	(<ol>	^state <state>)
 }

sp {elaborate*output-link*component
    :o-support
    (state <s>    ^name jarvis
                  ^world <w>
                  ^io <io>)
    (<io>    ^output-link.state <ol>
             ^input-link <il>)
    (<il>	^time <t>
          	^interaction.request get-all)
    (<w>    ^component <c>)
    (<c>    ^id <id>
            ^name <name>)
-->
    (<ol>    ^component <oc>)
    (<oc>    ^id <id>
             ^name <name>)
    }

sp {elaborate*output-link*state
    :o-support
    (state <s>    ^name jarvis
                  ^world <w>
                   ^io <io>)
    (<io>    ^output-link.state.component <oc>
             ^input-link <il>)
    (<il>	^time <t>
          	^interaction.request get-all)
    (<w>    ^component <c>)
    (<c>    ^{<state-var> <> name <> visible <> paired-with <> id}.value <val>
            ^id <id>)
    (<oc>    ^id <id>)
-->
    (<oc>    ^<state-var> <val>)
    }

sp {elaborate*output-link*visible
    :o-support
    (state <s>    ^name jarvis
                  ^world <w>
                   ^io <io>)
    (<io>    ^output-link.state.component <oc>
             ^input-link <il>)
    (<il>	^time <t>
          	^interaction.request get-all)
    (<w>    ^component <c>)
    (<c>    ^visible <v>
            ^id <id>)
    (<oc>    ^id <id>)
-->
    (<oc>    ^visible <v>)
    }
                  